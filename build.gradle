plugins {
    // Apply the org.jetbrains.kotlin.jvm Plugin to add support for Kotlin.
    id 'org.jetbrains.kotlin.jvm' version '1.7.20'

    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'

    id "com.github.ben-manes.versions" version "0.36.0"

    id "maven-publish"

    id "com.jfrog.bintray" version "1.8.5"
}

String currentVersion = '0.4.0'

group = 'io.supabase'
version = currentVersion

repositories {
    jcenter()
    mavenCentral()
}

dependencies {

    implementation 'org.apache.httpcomponents.client5:httpclient5:5.1.3'

    // Align versions of all Kotlin components
    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.12.1"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.1"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation "io.mockk:mockk:1.10.5"
    testImplementation 'com.willowtreeapps.assertk:assertk-jvm:0.23'
    testImplementation "com.github.tomakehurst:wiremock-jre8:2.27.2"

}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
    from javadoc.destinationDir
}

def pomConfig = {
    licenses {
        license {
            name "The MIT License"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "kevcodez"
            name "Kevin Gr√ºneberg"
            email "k.grueneberg1994@gmail.com"
        }
    }

    scm {
        url "https://github.com/supabase/gotrue-kt"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }

            groupId 'io.supabase'
            artifactId 'gotrue-kt'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Kotlin JVM client for Netlify\'s GoTrue API.')
                root.appendNode('name', 'GoTrue Kotlin')
                root.appendNode('url', 'https://github.com/supabase/gotrue-kt')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv("bintray_user") ?: findProperty("bintray_user")
    key = System.getenv("bintray_key") ?: findProperty("bintray_key")
    publications = ['mavenPublication']
    publish = true

    pkg {
        repo = 'supabase'
        name = 'gotrue-kt'
        userOrg = 'supabase'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/supabase/gotrue-kt.git'
        version {
            name = currentVersion
            desc = currentVersion
            released  = new Date()
        }
    }

}
